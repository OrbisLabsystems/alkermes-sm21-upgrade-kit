SET COMPILE_OPTION DECLARE

SET NAME "DEFER/"
ENABLE WINDOWS

JOIN LIBRARY $CRITERIA_EDIT
JOIN STANDARD_LIBRARY STD_CLASS
JOIN STANDARD_LIBRARY STD_ARRAY_SELECT

run_study_labels ()

ROUTINE run_study_labels

    DECLARE mandatory_array, selection_array, selection_array_db, count

    ARRAY mandatory_array
    ARRAY selection_array

    selection_array [ 1, 1 ] = ARRAY_SELECT_EQ
    selection_array [ 1, 2 ] = "HEADER"
    selection_array [ 1, 3 ] = ""

    selection_array [ 2, 1 ] = ARRAY_SELECT_AND

    selection_array [ 3, 1 ] = ARRAY_SELECT_EQ
    selection_array [ 3, 2 ] = "LAYER_FACTOR"
    selection_array [ 3, 3 ] = ""

    selection_array [ 4, 1 ] = ARRAY_SELECT_AND

    selection_array [ 5, 1 ] = ARRAY_SELECT_EQ
    selection_array [ 5, 2 ] = "TIME_FACTOR"
    selection_array [ 5, 3 ] = ""

    selection_array [ 6, 1 ] = ARRAY_SELECT_AND

    selection_array [ 7, 1 ] = ARRAY_SELECT_EQ
    selection_array [ 7, 2 ] = "C_FACTOR"
    selection_array [ 7, 3 ] = ""

    selection_array [ 8, 1 ] = ARRAY_SELECT_AND

    selection_array [ 9, 1 ] = ARRAY_SELECT_EQ
    selection_array [ 9, 2 ] = "PULL_NUM"
    selection_array [ 9, 3 ] = ""

    criteria_edit_criteria ( "pds_study_pull",
                             selection_array   ,
                             mandatory_array   ,
                             TRUE              ,
                             TRUE              )



    object_create (  selection_array_db, "STD_OBJECT_DATABASE" )
    selection_array_db . initialise ( "PDS_STUDY_PULL" )
    selection_array_db.select( selection_array )

    count = 0

    WHILE ( count < selection_array_db.size()) DO

        count = count + 1
        selection_array_db.set_by_number( count )

        print_pull_label ( selection_array_db . current )

    ENDWHILE

ENDROUTINE

ROUTINE print_pull_label ( pull_rec )

    DECLARE study, layer, time, c, no, cr_study_id, textual_samp_id

    study            =   SELECT pds_study_pull.header
                         IN OBJECT pull_rec

    layer            =   SELECT pds_study_pull.layer_factor
                         IN OBJECT pull_rec

    time             =   SELECT pds_study_pull.time_factor
                         IN OBJECT pull_rec

    c                =   SELECT pds_study_pull.c_factor
                         IN OBJECT pull_rec

    no               =   JUSTIFY ( PAD ( SELECT pds_study_pull.pull_num IN OBJECT pull_rec, " ", "10" ), "RIGHT" )

    cr_study_id      =   SELECT pds_study_pull.created_sample_id
                         IN OBJECT pull_rec

    textual_samp_id  =   SELECT pds_study_pull.sample_id_text
                         IN OBJECT pull_rec

LITERAL

Study               :   $study______________________
Layer               :   $layer______________________
Time Factor         :   $time_______________________
C Factor            :   $c__________________________
Pull Number         :   $no_________________________
Created Sample ID   :   $cr_study_id________________
Sample Text ID      :   $textual_samp_id____________

$ENDLITERAL

ENDROUTINE
